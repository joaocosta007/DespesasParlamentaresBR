USE Analise

DROP TABLE IF EXISTS #temp_candidatos

CREATE TABLE #temp_candidatos (
    DT_GERACAO NVARCHAR(MAX), HH_GERACAO NVARCHAR(MAX), ANO_ELEICAO NVARCHAR(MAX), CD_TIPO_ELEICAO NVARCHAR(MAX), NM_TIPO_ELEICAO NVARCHAR(MAX), NR_TURNO NVARCHAR(MAX),
    CD_ELEICAO NVARCHAR(MAX), DS_ELEICAO NVARCHAR(MAX), DT_ELEICAO NVARCHAR(MAX), TP_ABRANGENCIA NVARCHAR(MAX), SG_UF NVARCHAR(MAX), SG_UE NVARCHAR(MAX), NM_UE NVARCHAR(MAX) ,
    CD_CARGO NVARCHAR(MAX), DS_CARGO NVARCHAR(MAX), SQ_CANDIDATO NVARCHAR(MAX), NR_CANDIDATO NVARCHAR(MAX), NM_CANDIDATO NVARCHAR(MAX), NM_URNA_CANDIDATO NVARCHAR(MAX),
    NM_SOCIAL_CANDIDATO NVARCHAR(MAX), NR_CPF_CANDIDATO NVARCHAR(MAX), DS_EMAIL NVARCHAR(MAX), CD_SITUACAO_CANDIDATURA NVARCHAR(MAX), DS_SITUACAO_CANDIDATURA NVARCHAR(MAX),
    TP_AGREMIACAO NVARCHAR(MAX), NR_PARTIDO NVARCHAR(MAX), SG_PARTIDO NVARCHAR(MAX), NM_PARTIDO NVARCHAR(MAX), NR_FEDERACAO NVARCHAR(MAX), NM_FEDERACAO NVARCHAR(MAX),
    SG_FEDERACAO NVARCHAR(MAX), DS_COMPOSICAO_FEDERACAO NVARCHAR(MAX), SQ_COLIGACAO NVARCHAR(MAX), NM_COLIGACAO NVARCHAR(MAX), DS_COMPOSICAO_COLIGACAO NVARCHAR(MAX),
    SG_UF_NASCIMENTO NVARCHAR(MAX), DT_NASCIMENTO NVARCHAR(MAX), NR_TITULO_ELEITORAL_CANDIDATO NVARCHAR(MAX), CD_GENERO NVARCHAR(MAX), DS_GENERO NVARCHAR(MAX),
    CD_GRAU_INSTRUCAO NVARCHAR(MAX),DS_GRAU_INSTRUCAO NVARCHAR(MAX), CD_ESTADO_CIVIL NVARCHAR(MAX), DS_ESTADO_CIVIL NVARCHAR(MAX), CD_COR_RACA NVARCHAR(MAX),
    DS_COR_RACA NVARCHAR(MAX), CD_OCUPACAO NVARCHAR(MAX), DS_OCUPACAO NVARCHAR(MAX), CD_SIT_TOT_TURNO NVARCHAR(MAX), DS_SIT_TOT_TURNO NVARCHAR(MAX) 
);
GO

BULK INSERT #temp_candidatos
FROM '/tmp/bulk/consulta_cand_2022_BRASIL.csv'
WITH (
    FIELDTERMINATOR = ';',
    ROWTERMINATOR = '\n',
    FIRSTROW = 2,
    MAXERRORS = 1000
);
GO

DROP TABLE IF EXISTS #temp_votacao

CREATE TABLE #temp_votacao (
    DT_GERACAO NVARCHAR(MAX), HH_GERACAO NVARCHAR(MAX), ANO_ELEICAO NVARCHAR(MAX), CD_TIPO_ELEICAO NVARCHAR(MAX), NM_TIPO_ELEICAO NVARCHAR(MAX), NR_TURNO NVARCHAR(MAX),
    CD_ELEICAO NVARCHAR(MAX), DS_ELEICAO NVARCHAR(MAX), DT_ELEICAO NVARCHAR(MAX), TP_ABRANGENCIA NVARCHAR(MAX), SG_UF NVARCHAR(MAX), SG_UE NVARCHAR(MAX),
    NM_UE NVARCHAR(MAX), CD_MUNICIPIO NVARCHAR(MAX), NM_MUNICIPIO NVARCHAR(MAX), NR_ZONA NVARCHAR(MAX), NR_SECAO NVARCHAR(MAX), CD_CARGO NVARCHAR(MAX),
    DS_CARGO NVARCHAR(MAX), QT_APTOS NVARCHAR(MAX), QT_COMPARECIMENTO NVARCHAR(MAX), QT_ABSTENCOES NVARCHAR(MAX), QT_VOTOS_NOMINAIS NVARCHAR(MAX),
    QT_VOTOS_BRANCOS NVARCHAR(MAX), QT_VOTOS_NULOS NVARCHAR(MAX), QT_VOTOS_LEGENDA NVARCHAR(MAX), QT_VOTOS_ANULADOS_APU_SEP NVARCHAR(MAX), NR_LOCAL_VOTACAO NVARCHAR(MAX),
    NM_LOCAL_VOTACAO NVARCHAR(MAX), DS_LOCAL_VOTACAO_ENDERECO NVARCHAR(MAX)
);
GO

BULK INSERT #temp_votacao
FROM '/tmp/bulk/detalhe_votacao_secao_2022_BRASIL.csv'
WITH (
    FIELDTERMINATOR = ';',
    ROWTERMINATOR = '\n',
    FIRSTROW = 2,
    MAXERRORS = 1000
);
GO

DROP TABLE IF EXISTS #temp_bens

CREATE TABLE #temp_bens (
    DT_GERACAO NVARCHAR(MAX), HH_GERACAO NVARCHAR(MAX), ANO_ELEICAO NVARCHAR(MAX), CD_TIPO_ELEICAO NVARCHAR(MAX), NM_TIPO_ELEICAO NVARCHAR(MAX), CD_ELEICAO NVARCHAR(MAX),
    DS_ELEICAO NVARCHAR(MAX), DT_ELEICAO NVARCHAR(MAX), SG_UF NVARCHAR(MAX), SG_UE NVARCHAR(MAX), NM_UE NVARCHAR(MAX), SQ_CANDIDATO NVARCHAR(MAX),NR_ORDEM_BEM_CANDIDATO NVARCHAR(MAX),
    CD_TIPO_BEM_CANDIDATO NVARCHAR(MAX),DS_TIPO_BEM_CANDIDATO NVARCHAR(MAX), DS_BEM_CANDIDATO NVARCHAR(MAX),VR_BEM_CANDIDATO NVARCHAR(MAX), DT_ULT_ATUAL_BEM_CANDIDATO NVARCHAR(MAX),
    HH_ULT_ATUAL_BEM_CANDIDATO NVARCHAR(MAX)
);

BULK INSERT #temp_bens
FROM '/tmp/bulk/bem_candidato_2022_BRASIL.csv'
WITH (
    FIELDTERMINATOR = ';',
    ROWTERMINATOR = '\n',
    FIRSTROW = 2,
    MAXERRORS = 1000
);

DROP TABLE IF EXISTS #temp_coligacoes

CREATE TABLE #temp_coligacoes (
    DT_GERACAO NVARCHAR(MAX), HH_GERACAO NVARCHAR(MAX), ANO_ELEICAO NVARCHAR(MAX), CD_TIPO_ELEICAO NVARCHAR(MAX), NM_TIPO_ELEICAO NVARCHAR(MAX), NR_TURNO NVARCHAR(MAX),
    CD_ELEICAO NVARCHAR(MAX), DS_ELEICAO NVARCHAR(MAX), DT_ELEICAO NVARCHAR(MAX), SG_UF NVARCHAR(MAX), SG_UE NVARCHAR(MAX), NM_UE NVARCHAR(MAX), CD_CARGO NVARCHAR(MAX),
    DS_CARGO NVARCHAR(MAX), TP_AGREMIACAO NVARCHAR(MAX), NR_PARTIDO NVARCHAR(MAX), SG_PARTIDO NVARCHAR(MAX), NM_PARTIDO NVARCHAR(MAX), NR_FEDERACAO NVARCHAR(MAX),
    NM_FEDERACAO NVARCHAR(MAX), SG_FEDERACAO NVARCHAR(MAX), DS_COMPOSICAO_FEDERACAO NVARCHAR(MAX), SQ_COLIGACAO NVARCHAR(MAX), NM_COLIGACAO NVARCHAR(MAX),
    DS_COMPOSICAO_COLIGACAO NVARCHAR(MAX), CD_SITUACAO_LEGENDA NVARCHAR(MAX), DS_SITUACAO NVARCHAR(MAX), NM_TIPO_DESTINACAO_VOTOS NVARCHAR(MAX)  
);

BULK INSERT #temp_coligacoes
FROM '/tmp/bulk/consulta_coligacao_2022_BRASIL.csv'
WITH (
    FIELDTERMINATOR = ';',
    ROWTERMINATOR = '\n',
    FIRSTROW = 2,
    MAXERRORS = 1000
);
GO

DROP TABLE IF EXISTS #temp_despesas_contratadas

CREATE TABLE #temp_despesas_contratadas (
    DT_GERACAO NVARCHAR(MAX), HH_GERACAO NVARCHAR(MAX), AA_ELEICAO NVARCHAR(MAX), CD_TIPO_ELEICAO NVARCHAR(MAX), NM_TIPO_ELEICAO NVARCHAR(MAX), CD_ELEICAO NVARCHAR(MAX),
    DS_ELEICAO NVARCHAR(MAX), DT_ELEICAO NVARCHAR(MAX), ST_TURNO NVARCHAR(MAX), TP_PRESTACAO_CONTAS NVARCHAR(MAX), DT_PRESTACAO_CONTAS NVARCHAR(MAX),     
    SQ_PRESTADOR_CONTAS NVARCHAR(MAX), SG_UF NVARCHAR(MAX), SG_UE NVARCHAR(MAX), NM_UE NVARCHAR(MAX), NR_CNPJ_PRESTADOR_CONTA NVARCHAR(MAX), CD_CARGO NVARCHAR(MAX),
    DS_CARGO NVARCHAR(MAX), SQ_CANDIDATO NVARCHAR(MAX), NR_CANDIDATO NVARCHAR(MAX), NM_CANDIDATO NVARCHAR(MAX), NR_CPF_CANDIDATO NVARCHAR(MAX), NR_CPF_VICE_CANDIDATO NVARCHAR(MAX),   
    NR_PARTIDO NVARCHAR(MAX), SG_PARTIDO NVARCHAR(MAX), NM_PARTIDO NVARCHAR(MAX), CD_TIPO_FORNECEDOR NVARCHAR(MAX), DS_TIPO_FORNECEDOR NVARCHAR(MAX), CD_CNAE_FORNECEDOR NVARCHAR(MAX),      
    DS_CNAE_FORNECEDOR NVARCHAR(MAX), NR_CPF_CNPJ_FORNECEDOR NVARCHAR(MAX), NM_FORNECEDOR NVARCHAR(MAX), NM_FORNECEDOR_RFB NVARCHAR(MAX), CD_ESFERA_PART_FORNECEDOR NVARCHAR(MAX), 
    DS_ESFERA_PART_FORNECEDOR NVARCHAR(MAX), SG_UF_FORNECEDOR NVARCHAR(MAX), CD_MUNICIPIO_FORNECEDOR NVARCHAR(MAX), NM_MUNICIPIO_FORNECEDOR NVARCHAR(MAX), SQ_CANDIDATO_FORNECEDOR NVARCHAR(MAX), 
    NR_CANDIDATO_FORNECEDOR NVARCHAR(MAX), CD_CARGO_FORNECEDOR NVARCHAR(MAX), DS_CARGO_FORNECEDOR NVARCHAR(MAX), NR_PARTIDO_FORNECEDOR NVARCHAR(MAX), SG_PARTIDO_FORNECEDOR NVARCHAR(MAX),   
    NM_PARTIDO_FORNECEDOR NVARCHAR(MAX), DS_TIPO_DOCUMENTO NVARCHAR(MAX), NR_DOCUMENTO NVARCHAR(MAX), CD_ORIGEM_DESPESA NVARCHAR(MAX), DS_ORIGEM_DESPESA NVARCHAR(MAX), 
    SQ_DESPESA NVARCHAR(MAX), DT_DESPESA NVARCHAR(MAX), DS_DESPESA NVARCHAR(MAX), VR_DESPESA_CONTRATADA NVARCHAR(MAX)    
);
GO

BULK INSERT #temp_despesas_contratadas
FROM '/tmp/bulk/despesas_contratadas_candidatos_2022_BRASIL.csv'
WITH (
    FIELDTERMINATOR = ';',
    ROWTERMINATOR = '\n',
    FIRSTROW = 2,
    MAXERRORS = 1000
);
GO

DROP TABLE IF EXISTS #temp_receitas 

CREATE TABLE #temp_receitas (
    DT_GERACAO NVARCHAR(MAX), HH_GERACAO NVARCHAR(MAX), AA_ELEICAO NVARCHAR(MAX), CD_TIPO_ELEICAO NVARCHAR(MAX),
    NM_TIPO_ELEICAO NVARCHAR(MAX), CD_ELEICAO NVARCHAR(MAX), DS_ELEICAO NVARCHAR(MAX), DT_ELEICAO NVARCHAR(MAX),
    ST_TURNO NVARCHAR(MAX), TP_PRESTACAO_CONTAS NVARCHAR(MAX), DT_PRESTACAO_CONTAS NVARCHAR(MAX), SQ_PRESTADOR_CONTAS NVARCHAR(MAX),
    SG_UF NVARCHAR(MAX), SG_UE NVARCHAR(MAX),NM_UE NVARCHAR(MAX), NR_CNPJ_PRESTADOR_CONTA NVARCHAR(MAX),
    CD_CARGO NVARCHAR(MAX), DS_CARGO NVARCHAR(MAX), SQ_CANDIDATO NVARCHAR(MAX), NR_CANDIDATO NVARCHAR(MAX),
    NM_CANDIDATO NVARCHAR(MAX), NR_CPF_CANDIDATO NVARCHAR(MAX), NR_CPF_VICE_CANDIDATO NVARCHAR(MAX), NR_PARTIDO NVARCHAR(MAX),
    SG_PARTIDO NVARCHAR(MAX), NM_PARTIDO NVARCHAR(MAX), CD_FONTE_RECEITA NVARCHAR(MAX), DS_FONTE_RECEITA NVARCHAR(MAX),           
    CD_ORIGEM_RECEITA NVARCHAR(MAX), DS_ORIGEM_RECEITA NVARCHAR(MAX), CD_NATUREZA_RECEITA NVARCHAR(MAX), DS_NATUREZA_RECEITA NVARCHAR(MAX),        
    CD_ESPECIE_RECEITA NVARCHAR(MAX), DS_ESPECIE_RECEITA NVARCHAR(MAX), CD_CNAE_DOADOR NVARCHAR(MAX), DS_CNAE_DOADOR NVARCHAR(MAX),             
    NR_CPF_CNPJ_DOADOR NVARCHAR(MAX), NM_DOADOR NVARCHAR(MAX), NM_DOADOR_RFB NVARCHAR(MAX), CD_ESFERA_PARTIDARIA_DOADOR NVARCHAR(MAX),
    DS_ESFERA_PARTIDARIA_DOADOR NVARCHAR(MAX), SG_UF_DOADOR NVARCHAR(MAX), CD_MUNICIPIO_DOADOR NVARCHAR(MAX), NM_MUNICIPIO_DOADOR NVARCHAR(MAX),        
    SQ_CANDIDATO_DOADOR NVARCHAR(MAX), NR_CANDIDATO_DOADOR NVARCHAR(MAX), CD_CARGO_CANDIDATO_DOADOR NVARCHAR(MAX), DS_CARGO_CANDIDATO_DOADOR NVARCHAR(MAX),  
    NR_PARTIDO_DOADOR NVARCHAR(MAX), SG_PARTIDO_DOADOR NVARCHAR(MAX), NM_PARTIDO_DOADOR NVARCHAR(MAX), NR_RECIBO_DOACAO NVARCHAR(MAX),           
    NR_DOCUMENTO_DOACAO NVARCHAR(MAX), SQ_RECEITA NVARCHAR(MAX), DT_RECEITA NVARCHAR(MAX), DS_RECEITA NVARCHAR(MAX),                 
    VR_RECEITA NVARCHAR(MAX), DS_NATUREZA_RECURSO_ESTIMAVEL NVARCHAR(MAX), DS_GENERO NVARCHAR(MAX), DS_COR_RACA NVARCHAR(MAX)                 
);
GO

BULK INSERT #temp_receitas
FROM '/tmp/bulk/receitas_candidatos_2022_BRASIL.csv'
WITH (
    FIELDTERMINATOR = ';',
    ROWTERMINATOR = '\n',
    FIRSTROW = 2,
    MAXERRORS = 1000
);
GO

DROP TABLE IF EXISTS #temp_despesas_pagas

CREATE TABLE #temp_despesas_pagas (
    DT_GERACAO NVARCHAR(MAX), HH_GERACAO NVARCHAR(MAX), AA_ELEICAO NVARCHAR(MAX), CD_TIPO_ELEICAO NVARCHAR(MAX),
    NM_TIPO_ELEICAO NVARCHAR(MAX), CD_ELEICAO NVARCHAR(MAX),DS_ELEICAO NVARCHAR(MAX), DT_ELEICAO NVARCHAR(MAX),
    ST_TURNO NVARCHAR(MAX), TP_PRESTACAO_CONTAS NVARCHAR(MAX), DT_PRESTACAO_CONTAS NVARCHAR(MAX), SQ_PRESTADOR_CONTAS NVARCHAR(MAX),
    SG_UF NVARCHAR(MAX), DS_TIPO_DOCUMENTO NVARCHAR(MAX), NR_DOCUMENTO NVARCHAR(MAX), CD_FONTE_DESPESA NVARCHAR(MAX),         
    DS_FONTE_DESPESA NVARCHAR(MAX), CD_ORIGEM_DESPESA NVARCHAR(MAX), DS_ORIGEM_DESPESA NVARCHAR(MAX), CD_NATUREZA_DESPESA NVARCHAR(MAX),      
    DS_NATUREZA_DESPESA NVARCHAR(MAX), CD_ESPECIE_RECURSO NVARCHAR(MAX), DS_ESPECIE_RECURSO NVARCHAR(MAX), SQ_DESPESA NVARCHAR(MAX),
    SQ_PARCELAMENTO_DESPESA NVARCHAR(MAX), DT_PAGTO_DESPESA NVARCHAR(MAX), DS_DESPESA NVARCHAR(MAX), VR_PAGTO_DESPESA NVARCHAR(MAX)          
);

BULK INSERT #temp_despesas_pagas
FROM '/tmp/bulk/despesas_pagas_candidatos_2022_BRASIL.csv'
WITH (
    FIELDTERMINATOR = ';',
    ROWTERMINATOR = '\n',
    FIRSTROW = 2,
    MAXERRORS = 1000
);

DROP TABLE IF EXISTS #temp_doadores_originarios

CREATE TABLE #temp_doadores_originarios (
    DT_GERACAO NVARCHAR(MAX), HH_GERACAO NVARCHAR(MAX), AA_ELEICAO NVARCHAR(MAX), CD_TIPO_ELEICAO NVARCHAR(MAX),
    NM_TIPO_ELEICAO NVARCHAR(MAX),CD_ELEICAO NVARCHAR(MAX), DS_ELEICAO NVARCHAR(MAX), DT_ELEICAO NVARCHAR(MAX),
    ST_TURNO NVARCHAR(MAX), TP_PRESTACAO_CONTAS NVARCHAR(MAX), DT_PRESTACAO_CONTAS NVARCHAR(MAX), SQ_PRESTADOR_CONTAS NVARCHAR(MAX),
    SG_UF NVARCHAR(MAX), NR_CPF_CNPJ_DOADOR_ORIGINARIO NVARCHAR(MAX), NM_DOADOR_ORIGINARIO NVARCHAR(MAX), NM_DOADOR_ORIGINARIO_RFB NVARCHAR(MAX),      
    TP_DOADOR_ORIGINARIO NVARCHAR(MAX), CD_CNAE_DOADOR_ORIGINARIO NVARCHAR(MAX), DS_CNAE_DOADOR_ORIGINARIO NVARCHAR(MAX), SQ_RECEITA NVARCHAR(MAX),
    DT_RECEITA NVARCHAR(MAX), DS_RECEITA NVARCHAR(MAX),VR_RECEITA NVARCHAR(MAX)
);

BULK INSERT #temp_doadores_originarios
FROM '/tmp/bulk/receitas_candidatos_doador_originario_2022_BRASIL.csv'
WITH (
    FIELDTERMINATOR = ';',
    ROWTERMINATOR = '\n',
    FIRSTROW = 2,
    MAXERRORS = 1000
);


INSERT INTO Dim_Tempo (Data_ID, Data_Completa, Ano, Mes, Dia, Trimestre, Dia_Semana)
SELECT DISTINCT
    CONVERT(INT, CONVERT(VARCHAR(8), Data_Value, 112)) AS Data_ID,
    Data_Value AS Data_Completa,
    YEAR(Data_Value) AS Ano,
    MONTH(Data_Value) AS Mes,
    DAY(Data_Value) AS Dia,
    DATEPART(QUARTER, Data_Value) AS Trimestre,
    DATENAME(WEEKDAY, Data_Value) AS Dia_Semana
FROM (

    SELECT DISTINCT CASE WHEN ISDATE(DT_ELEICAO) = 1 THEN CAST(DT_ELEICAO AS DATE) ELSE NULL END AS Data_Value 
    FROM #temp_candidatos 
    WHERE DT_ELEICAO IS NOT NULL AND DT_ELEICAO NOT IN ('#NULO', '#NE', 'NÃO DIVULGÁVEL', '-1', '-3', '-4')
    
    UNION

    SELECT DISTINCT CASE WHEN ISDATE(DT_NASCIMENTO) = 1 THEN CAST(DT_NASCIMENTO AS DATE) ELSE NULL END AS Data_Value 
    FROM #temp_candidatos 
    WHERE DT_NASCIMENTO IS NOT NULL AND DT_NASCIMENTO NOT IN ('#NULO', '#NE', 'NÃO DIVULGÁVEL', '-1', '-3', '-4')
    
    UNION

    SELECT DISTINCT CASE WHEN ISDATE(DT_DESPESA) = 1 THEN CAST(DT_DESPESA AS DATE) ELSE NULL END AS Data_Value 
    FROM #temp_despesas_contratadas 
    WHERE DT_DESPESA IS NOT NULL AND DT_DESPESA NOT IN ('#NULO', '#NE', 'NÃO DIVULGÁVEL', '-1', '-3', '-4')
    
    UNION

    SELECT DISTINCT CASE WHEN ISDATE(DT_RECEITA) = 1 THEN CAST(DT_RECEITA AS DATE) ELSE NULL END AS Data_Value 
    FROM #temp_receitas 
    WHERE DT_RECEITA IS NOT NULL AND DT_RECEITA NOT IN ('#NULO', '#NE', 'NÃO DIVULGÁVEL', '-1', '-3', '-4')
    
    UNION

    SELECT DISTINCT CASE WHEN ISDATE(DT_PAGTO_DESPESA) = 1 THEN CAST(DT_PAGTO_DESPESA AS DATE) ELSE NULL END AS Data_Value 
    FROM #temp_despesas_pagas 
    WHERE DT_PAGTO_DESPESA IS NOT NULL AND DT_PAGTO_DESPESA NOT IN ('#NULO', '#NE', 'NÃO DIVULGÁVEL', '-1', '-3', '-4')
) AS Todas_Datas
WHERE Data_Value IS NOT NULL;


INSERT INTO Dim_UF (SG_UF, NM_UF, Regiao)
VALUES 
    ('AC', 'Acre', 'Norte'), ('AL', 'Alagoas', 'Nordeste'), ('AP', 'Amapá', 'Norte'),
    ('AM', 'Amazonas', 'Norte'), ('BA', 'Bahia', 'Nordeste'), ('CE', 'Ceará', 'Nordeste'),
    ('DF', 'Distrito Federal', 'Centro-Oeste'), ('ES', 'Espírito Santo', 'Sudeste'),
    ('GO', 'Goiás', 'Centro-Oeste'), ('MA', 'Maranhão', 'Nordeste'), ('MT', 'Mato Grosso', 'Centro-Oeste'),
    ('MS', 'Mato Grosso do Sul', 'Centro-Oeste'), ('MG', 'Minas Gerais', 'Sudeste'),
    ('PA', 'Pará', 'Norte'), ('PB', 'Paraíba', 'Nordeste'), ('PR', 'Paraná', 'Sul'),
    ('PE', 'Pernambuco', 'Nordeste'), ('PI', 'Piauí', 'Nordeste'), ('RJ', 'Rio de Janeiro', 'Sudeste'),
    ('RN', 'Rio Grande do Norte', 'Nordeste'), ('RS', 'Rio Grande do Sul', 'Sul'),
    ('RO', 'Rondônia', 'Norte'), ('RR', 'Roraima', 'Norte'), ('SC', 'Santa Catarina', 'Sul'),
    ('SP', 'São Paulo', 'Sudeste'), ('SE', 'Sergipe', 'Nordeste'), ('TO', 'Tocantins', 'Norte'),
    ('BR', 'Brasil', 'Nacional'), ('ZZ', 'Exterior', 'Exterior'), ('VT', 'Voto em Trânsito', 'Trânsito');


INSERT INTO Dim_Cargo (CD_CARGO, DS_CARGO)
SELECT DISTINCT 
    CAST(CD_CARGO AS INT),
    DS_CARGO
FROM #temp_candidatos
WHERE ISNUMERIC(CD_CARGO) = 1 AND CD_CARGO NOT IN ('-1', '-3', '-4');


INSERT INTO Dim_Partido (NR_PARTIDO, SG_PARTIDO, NM_PARTIDO)
SELECT DISTINCT 
    CAST(NR_PARTIDO AS INT),
    SG_PARTIDO,
    NM_PARTIDO
FROM #temp_candidatos
WHERE ISNUMERIC(NR_PARTIDO) = 1 AND NR_PARTIDO NOT IN ('-1', '-3', '-4');


INSERT INTO Dim_Municipio (CD_MUNICIPIO, NM_MUNICIPIO, UF_ID)
SELECT DISTINCT 
    CAST(tv.CD_MUNICIPIO AS INT),
    tv.NM_MUNICIPIO,
    u.UF_ID
FROM #temp_votacao tv
JOIN Dim_UF u ON tv.SG_UF = u.SG_UF
WHERE ISNUMERIC(tv.CD_MUNICIPIO) = 1 
  AND tv.CD_MUNICIPIO NOT IN ('-1', '-3', '-4')
  AND tv.NM_MUNICIPIO IS NOT NULL;


INSERT INTO Dim_Candidato (
    SQ_CANDIDATO, NM_URNA_CANDIDATO, NM_CANDIDATO, NR_CANDIDATO,
    UF_ID, Cargo_ID, Partido_ID, DS_GENERO, DS_COR_RACA,
    DS_GRAU_INSTRUCAO, DS_OCUPACAO, DT_NASCIMENTO,
    DS_SITUACAO_CANDIDATURA, DS_SIT_TOT_TURNO
)
SELECT DISTINCT
    CAST(tc.SQ_CANDIDATO AS BIGINT),
    tc.NM_URNA_CANDIDATO,
    tc.NM_CANDIDATO,
    tc.NR_CANDIDATO,
    u.UF_ID,
    c.Cargo_ID,
    p.Partido_ID,
    CASE WHEN tc.DS_GENERO IN ('#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL ELSE tc.DS_GENERO END,
    CASE WHEN tc.DS_COR_RACA IN ('#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL ELSE tc.DS_COR_RACA END,
    CASE WHEN tc.DS_GRAU_INSTRUCAO IN ('#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL ELSE tc.DS_GRAU_INSTRUCAO END,
    CASE WHEN tc.DS_OCUPACAO IN ('#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL ELSE tc.DS_OCUPACAO END,
    CASE 
        WHEN tc.DT_NASCIMENTO IN ('#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL
        WHEN ISDATE(tc.DT_NASCIMENTO) = 1 THEN CAST(tc.DT_NASCIMENTO AS DATE)
        ELSE NULL
    END,
    tc.DS_SITUACAO_CANDIDATURA,
    tc.DS_SIT_TOT_TURNO
FROM #temp_candidatos tc
JOIN Dim_UF u ON tc.SG_UF = u.SG_UF
JOIN Dim_Cargo c ON CAST(tc.CD_CARGO AS INT) = c.CD_CARGO
JOIN Dim_Partido p ON CAST(tc.NR_PARTIDO AS INT) = p.NR_PARTIDO
WHERE ISNUMERIC(tc.SQ_CANDIDATO) = 1 
  AND tc.SQ_CANDIDATO NOT IN ('-1', '-3', '-4');


INSERT INTO Dim_Fornecedor (NR_CPF_CNPJ_FORNECEDOR, NM_FORNECEDOR, DS_TIPO_FORNECEDOR)
SELECT DISTINCT 
    tdc.NR_CPF_CNPJ_FORNECEDOR,
    tdc.NM_FORNECEDOR,
    tdc.DS_TIPO_FORNECEDOR
FROM #temp_despesas_contratadas tdc
WHERE 
    tdc.NM_FORNECEDOR IS NOT NULL 
    AND tdc.NM_FORNECEDOR NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL', 'NAO DIVULGAVEL')
    AND LTRIM(RTRIM(tdc.NM_FORNECEDOR)) != ''
    AND tdc.NM_FORNECEDOR NOT LIKE '-%'
    AND tdc.DS_TIPO_FORNECEDOR IS NOT NULL
    AND tdc.DS_TIPO_FORNECEDOR NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL');


INSERT INTO Dim_Doador (NR_CPF_CNPJ_DOADOR, NM_DOADOR, DS_TIPO_DOADOR)
SELECT DISTINCT 
    tr.NR_CPF_CNPJ_DOADOR,
    tr.NM_DOADOR,
    CASE 
        WHEN tr.NR_CPF_CNPJ_DOADOR LIKE '[0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9]' THEN 'Pessoa Física'
        WHEN tr.NR_CPF_CNPJ_DOADOR LIKE '[0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9]' THEN 'Pessoa Jurídica'
        ELSE 'Outros'
    END AS DS_TIPO_DOADOR
FROM #temp_receitas tr
WHERE 
    tr.NM_DOADOR IS NOT NULL 
    AND tr.NM_DOADOR NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL', 'NAO DIVULGAVEL')
    AND LTRIM(RTRIM(tr.NM_DOADOR)) != ''
    AND tr.NM_DOADOR NOT LIKE '-%'
    AND (tr.NR_CPF_CNPJ_DOADOR IS NULL 
         OR tr.NR_CPF_CNPJ_DOADOR NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL')
         OR tr.NR_CPF_CNPJ_DOADOR LIKE '[0-9]%'
         OR LEN(LTRIM(RTRIM(tr.NR_CPF_CNPJ_DOADOR))) > 3);


INSERT INTO Dim_Tipo_Despesa (DS_ORIGEM_DESPESA, DS_NATUREZA_DESPESA)
SELECT DISTINCT 
    tdp.DS_ORIGEM_DESPESA,
    tdp.DS_NATUREZA_DESPESA
FROM #temp_despesas_pagas tdp
WHERE 
    tdp.DS_ORIGEM_DESPESA IS NOT NULL 
    AND tdp.DS_ORIGEM_DESPESA NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL')
    AND LTRIM(RTRIM(tdp.DS_ORIGEM_DESPESA)) != '';


INSERT INTO Dim_Tipo_Despesa_Contratada (DS_ORIGEM_DESPESA, DS_TIPO_FORNECEDOR)
SELECT DISTINCT 
    tdc.DS_ORIGEM_DESPESA,
    tdc.DS_TIPO_FORNECEDOR
FROM #temp_despesas_contratadas tdc
WHERE 
    tdc.DS_ORIGEM_DESPESA IS NOT NULL 
    AND tdc.DS_ORIGEM_DESPESA NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL')
    AND LTRIM(RTRIM(tdc.DS_ORIGEM_DESPESA)) != '';


INSERT INTO Dim_Tipo_Receita (DS_ORIGEM_RECEITA, DS_FONTE_RECEITA)
SELECT DISTINCT 
    tr.DS_ORIGEM_RECEITA,
    tr.DS_FONTE_RECEITA
FROM #temp_receitas tr
WHERE 
    tr.DS_ORIGEM_RECEITA IS NOT NULL 
    AND tr.DS_ORIGEM_RECEITA NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL')
    AND LTRIM(RTRIM(tr.DS_ORIGEM_RECEITA)) != '';


INSERT INTO Fact_Bens (Candidato_ID, Data_ID, DS_TIPO_BEM_CANDIDATO, DS_BEM_CANDIDATO, VR_BEM_CANDIDATO)
SELECT 
    c.Candidato_ID,
    t.Data_ID,
    tb.DS_TIPO_BEM_CANDIDATO,
    tb.DS_BEM_CANDIDATO,
    CASE 
        WHEN tb.VR_BEM_CANDIDATO IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL
        WHEN tb.VR_BEM_CANDIDATO IS NULL OR LTRIM(RTRIM(tb.VR_BEM_CANDIDATO)) = '' THEN NULL
        WHEN TRY_CAST(REPLACE(REPLACE(LTRIM(RTRIM(tb.VR_BEM_CANDIDATO)), ',', '.'), ' ', '') AS DECIMAL(15,2)) IS NOT NULL 
        THEN TRY_CAST(REPLACE(REPLACE(LTRIM(RTRIM(tb.VR_BEM_CANDIDATO)), ',', '.'), ' ', '') AS DECIMAL(15,2))
        ELSE NULL
    END AS VR_BEM_CANDIDATO
FROM #temp_bens tb
JOIN Dim_Candidato c ON CAST(tb.SQ_CANDIDATO AS BIGINT) = c.SQ_CANDIDATO
LEFT JOIN Dim_Tempo t ON 
    CASE 
        WHEN ISDATE(tb.DT_ULT_ATUAL_BEM_CANDIDATO) = 1 
        THEN CAST(tb.DT_ULT_ATUAL_BEM_CANDIDATO AS DATE)
        ELSE NULL
    END = t.Data_Completa
WHERE 
    tb.DS_BEM_CANDIDATO IS NOT NULL 
    AND tb.DS_BEM_CANDIDATO NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL')
    AND LTRIM(RTRIM(tb.DS_BEM_CANDIDATO)) != '';


INSERT INTO Fact_Receitas (Candidato_ID, Doador_ID, Tipo_Receita_ID, Data_ID, VR_RECEITA, DS_RECEITA)
SELECT 
    c.Candidato_ID,
    d.Doador_ID,
    tr.Tipo_Receita_ID,
    t.Data_ID,
    CASE 
        WHEN trec.VR_RECEITA IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL
        WHEN trec.VR_RECEITA IS NULL OR LTRIM(RTRIM(trec.VR_RECEITA)) = '' THEN NULL
        WHEN TRY_CAST(REPLACE(REPLACE(LTRIM(RTRIM(trec.VR_RECEITA)), ',', '.'), ' ', '') AS DECIMAL(15,2)) IS NOT NULL 
        THEN TRY_CAST(REPLACE(REPLACE(LTRIM(RTRIM(trec.VR_RECEITA)), ',', '.'), ' ', '') AS DECIMAL(15,2))
        ELSE NULL
    END AS VR_RECEITA,
    CASE 
        WHEN trec.DS_RECEITA IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL
        WHEN trec.DS_RECEITA IS NULL OR LTRIM(RTRIM(trec.DS_RECEITA)) = '' THEN NULL
        ELSE trec.DS_RECEITA
    END AS DS_RECEITA
FROM #temp_receitas trec
JOIN Dim_Candidato c ON CAST(trec.SQ_CANDIDATO AS BIGINT) = c.SQ_CANDIDATO
LEFT JOIN Dim_Doador d ON trec.NR_CPF_CNPJ_DOADOR = d.NR_CPF_CNPJ_DOADOR AND trec.NM_DOADOR = d.NM_DOADOR
LEFT JOIN Dim_Tipo_Receita tr ON trec.DS_ORIGEM_RECEITA = tr.DS_ORIGEM_RECEITA AND trec.DS_FONTE_RECEITA = tr.DS_FONTE_RECEITA
LEFT JOIN Dim_Tempo t ON 
    CASE 
        WHEN ISDATE(trec.DT_RECEITA) = 1 
        THEN CAST(trec.DT_RECEITA AS DATE)
        ELSE NULL
    END = t.Data_Completa
WHERE 
    trec.VR_RECEITA IS NOT NULL 
    AND trec.VR_RECEITA NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL')
    AND LTRIM(RTRIM(trec.VR_RECEITA)) != ''
    AND trec.DS_RECEITA IS NOT NULL
    AND trec.DS_RECEITA NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL')
    AND LTRIM(RTRIM(trec.DS_RECEITA)) != '';


INSERT INTO Fact_Despesas (Candidato_ID, Fornecedor_ID, Tipo_Despesa_ID, Data_ID, VR_DESPESA_CONTRATADA, DS_DESPESA)
SELECT 
    c.Candidato_ID,
    f.Fornecedor_ID,
    td.Tipo_Despesa_ID,
    t.Data_ID,
    CASE 
        WHEN tdc.VR_DESPESA_CONTRATADA IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL
        WHEN tdc.VR_DESPESA_CONTRATADA IS NULL OR LTRIM(RTRIM(tdc.VR_DESPESA_CONTRATADA)) = '' THEN NULL
        WHEN TRY_CAST(REPLACE(REPLACE(LTRIM(RTRIM(tdc.VR_DESPESA_CONTRATADA)), ',', '.'), ' ', '') AS DECIMAL(15,2)) IS NOT NULL 
        THEN TRY_CAST(REPLACE(REPLACE(LTRIM(RTRIM(tdc.VR_DESPESA_CONTRATADA)), ',', '.'), ' ', '') AS DECIMAL(15,2))
        ELSE NULL
    END AS VR_DESPESA_CONTRATADA,
    CASE 
        WHEN tdc.DS_DESPESA IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL
        WHEN tdc.DS_DESPESA IS NULL OR LTRIM(RTRIM(tdc.DS_DESPESA)) = '' THEN NULL
        ELSE tdc.DS_DESPESA
    END AS DS_DESPESA
FROM #temp_despesas_contratadas tdc
JOIN Dim_Candidato c ON CAST(tdc.SQ_CANDIDATO AS BIGINT) = c.SQ_CANDIDATO
LEFT JOIN Dim_Fornecedor f ON tdc.NR_CPF_CNPJ_FORNECEDOR = f.NR_CPF_CNPJ_FORNECEDOR AND tdc.NM_FORNECEDOR = f.NM_FORNECEDOR
LEFT JOIN Dim_Tipo_Despesa td ON tdc.DS_ORIGEM_DESPESA = td.DS_ORIGEM_DESPESA
LEFT JOIN Dim_Tempo t ON 
    CASE 
        WHEN tdc.DT_DESPESA IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL
        WHEN ISDATE(tdc.DT_DESPESA) = 1 
        THEN CAST(tdc.DT_DESPESA AS DATE)
        ELSE NULL
    END = t.Data_Completa
WHERE 
    tdc.VR_DESPESA_CONTRATADA IS NOT NULL 
    AND tdc.VR_DESPESA_CONTRATADA NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL')
    AND LTRIM(RTRIM(tdc.VR_DESPESA_CONTRATADA)) != ''
    AND tdc.DS_DESPESA IS NOT NULL
    AND tdc.DS_DESPESA NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL')
    AND LTRIM(RTRIM(tdc.DS_DESPESA)) != '';


INSERT INTO Fact_Votacao (
    Municipio_ID, 
    Data_ID, 
    Cargo_ID, 
    Partido_ID,
    NR_ZONA, 
    QT_VOTOS_NOMINAIS, 
    QT_VOTOS_LEGENDA, 
    QT_APTOS, 
    QT_COMPARECIMENTO, 
    QT_ABSTENCOES
)
SELECT 
    m.Municipio_ID,
    t.Data_ID,
    car.Cargo_ID,
    p.Partido_ID,
    tv.NR_ZONA,
    CASE 
        WHEN tv.QT_VOTOS_NOMINAIS IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL
        WHEN TRY_CAST(tv.QT_VOTOS_NOMINAIS AS INT) IS NOT NULL THEN CAST(tv.QT_VOTOS_NOMINAIS AS INT)
        ELSE NULL
    END AS QT_VOTOS_NOMINAIS,
    
    CASE 
        WHEN tv.QT_VOTOS_LEGENDA IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL
        WHEN TRY_CAST(tv.QT_VOTOS_LEGENDA AS INT) IS NOT NULL THEN CAST(tv.QT_VOTOS_LEGENDA AS INT)
        ELSE NULL
    END AS QT_VOTOS_LEGENDA,
    
    CASE 
        WHEN tv.QT_APTOS IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL
        WHEN TRY_CAST(tv.QT_APTOS AS INT) IS NOT NULL THEN CAST(tv.QT_APTOS AS INT)
        ELSE NULL
    END AS QT_APTOS,
    
    CASE 
        WHEN tv.QT_COMPARECIMENTO IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL
        WHEN TRY_CAST(tv.QT_COMPARECIMENTO AS INT) IS NOT NULL THEN CAST(tv.QT_COMPARECIMENTO AS INT)
        ELSE NULL
    END AS QT_COMPARECIMENTO,
    
    CASE 
        WHEN tv.QT_ABSTENCOES IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL') THEN NULL
        WHEN TRY_CAST(tv.QT_ABSTENCOES AS INT) IS NOT NULL THEN CAST(tv.QT_ABSTENCOES AS INT)
        ELSE NULL
    END AS QT_ABSTENCOES

FROM #temp_votacao tv
INNER JOIN Dim_Municipio m ON tv.CD_MUNICIPIO = m.CD_MUNICIPIO
INNER JOIN Dim_Tempo t ON 
    CASE 
        WHEN ISDATE(tv.DT_ELEICAO) = 1 THEN CAST(tv.DT_ELEICAO AS DATE)
        ELSE NULL
    END = t.Data_Completa
INNER JOIN Dim_Cargo car ON tv.CD_CARGO = car.CD_CARGO
INNER JOIN Dim_Partido p ON 
    p.Partido_ID = (
        SELECT TOP 1 Partido_ID 
        FROM Dim_Candidato cand 
        WHERE cand.Cargo_ID = car.Cargo_ID 
        AND cand.UF_ID = m.UF_ID
        ORDER BY cand.Candidato_ID
    ) 
WHERE 
    tv.QT_APTOS IS NOT NULL 
    AND tv.QT_APTOS NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL')
    AND TRY_CAST(tv.QT_APTOS AS INT) > 0
    AND tv.QT_VOTOS_NOMINAIS IS NOT NULL
    AND tv.QT_VOTOS_NOMINAIS NOT IN ('-1', '-3', '-4', '#NULO', '#NE', 'NÃO DIVULGÁVEL');
    